# The 'prims' module provides a useful set of words for getting started with
# the g0 language. This includes defining all the basic Glas program primitives
# and several convenient utility functions that don't require loops.

macro apply []

# TRUE PRIMITIVES

prog copy ['copy apply]
prog swap ['swap apply]
prog drop ['drop apply]

prog eq ['eq apply]
prog eff ['eff apply]
prog fail ['fail apply]

prog get ['get apply]
prog put ['put apply]
prog del ['del apply]

prog p-dip [0 'dip put]
prog p-while-do [0 'do put 'while put 0 'loop put]
prog p-until-do [0 'do put 'until put 0 'loop put]
prog p-try-then-else [0 'else put 'then put 'try put 0 'cond put]
prog p-with-do [0 'do put 'with put 0 'env put]

macro dip [p-dip]
macro while-do [p-while-do]
macro until-do [p-until-do]
macro try-then-else [p-try-then-else]
macro with-do [p-with-do]

# CONDITIONAL VARIATIONS
# try       ([P]try == [P][][]try-then-else)
# try-then  ([P][L]try-then == [P][L][]try-then-else)
# try-else  ([P][R]try-else == [P][][R]try-then-else)
prog p-try [0 'try put 0 'cond put]
prog p-try-then [0 'then put 'try put 0 'cond put]
prog p-try-else [0 'else put 'try put 0 'cond put]
macro try [p-try]
macro try-then [p-try-then]
macro try-else [p-try-else]

# halt - explicit divergence
#  logically equivalent to entering an infinite loop, but makes this
#  intention more explicit. Unlike 'fail', it is not possible to catch
#  'halt' within the program under evaluation.
#
#  A debug message may be included such as 'halt:tbd' to indicate
#  operation is to be defined, or 'halt:type-error' to indicate 
#  this was a failed runtime type check.
#
prog p-halt-with-msg [0 'halt put]
macro halt-with-msg [p-halt-with-msg]
prog halt ['halt apply]
prog halt-tbd ['tbd halt-with-msg]
prog halt-type-error ['type-error halt-with-msg]
prog halt-unreachable ['unreachable halt-with-msg]

# Unit data.
#  The tree with a single node is used to represent unit, zero,
#  and empty records or lists.
data unit [0]

# PAIRS 
# 
#   (A,B) =>   /\
#             A  B
#
# Glas systems favor labeled records for extensibility and
# self-documentation. However, pairs are a useful low-level
# type. Lists are formed from pairs. 

prog pair [unit 0b1 put 0b0 put]
prog pair-fst [0b0 get]
prog pair-snd [0b1 get]
prog unpair [copy pair-fst swap pair-snd]
assert [1 2 3 pair pair pair-snd unpair 3 eq 2 eq]

# DATA AND SEQ MACRO SUPPORT
#
# No need for 'data' or 'seq' as macros, but p-* forms
# are convenient for construction of new macros.

# p-data    Value -- [Value]
prog p-data [0 'data put]

# p-seq     ([A] [B] -- [A B])
#  minor optimization when [B] is already a sequence.
prog p-seq  [['seq get] [0 pair] try-else pair 0 'seq put]

# p-prefix  ([A] [B] -- [B A])
#   same as 'swap p-seq'. Comes up a lot.
prog p-prefix [swap p-seq]

# p-suffix  ([A] [B] -- [A B])
#   p-seq but renamed to match p-prefix.
prog p-suffix [p-seq]

# p-curry   (Value [Fn] -- [Value Fn])
#  bind first parameter into a program value.
prog p-curry [[p-data] dip p-seq]

# p-nop     -- []
#  non-operation in Glas is empty seq
data p-nop ['seq]

assert [1 [2 pair] p-curry apply unpair 2 eq 1 eq]

# LOOP VARIATIONS
# loop          ([Op] loop)
#  run sub-program repeatedly until it fails.
prog p-loop [0 'while put 0 'loop put]
macro loop [p-loop]

# do-while      ([Body][Cond]do-while)
#  run Body once before checking condition. Equivalent: 
#    Body [Cond] [Body] while-do
prog p-do-while [[copy] dip swap p-while-do p-seq]
macro do-while [p-do-while]

# do-until      ([Body][Cond]do-until)
#  run Body once before checking condition. Equivalent:
#    Body [Cond] [Body] until-do
prog p-do-until [[copy] dip swap p-until-do p-seq]
macro do-until [p-do-until]

# NON-MODIFYING OBSERVATIONS
#
# The 'reject' and 'verify' operations both compute a predicate
# then abort, returning to the state prior to the computation.
# The only observable output is success or failure.
#

# reject ([Predicate] -- | FAIL)
#  will evaluate predicate then fail iff predicate passes.
prog p-reject [[fail] p-try-then]
macro reject [p-reject]
assert [0 [1 eq] reject 0 eq]
assert [0 [[0 eq] reject][1][2] try-then-else 2 eq 0 eq]
assert [[[] reject] reject]

# verify    ([Predicate] -- | FAIL)
#  will test predicate via evaluate then abort
#  passes if evaluation of predicate does not fail
prog p-verify [p-reject p-reject]
macro verify [p-verify]
assert [0 [0 eq] verify 0 eq]
assert [0 [[1 eq] verify][1][2] try-then-else 2 eq 0 eq]

# eq1       Val Val -- Val
#  keep a copy of the value on the equality test
prog eq1 [copy [eq] dip]
assert [0b0 0b0 eq1 0b0 eq]
assert [[0b0 0b1 eq1] reject]

# DATA PLUMBING SUPPORT
# 
# rot3      (X Y Z -- Y Z X)        rotate 3rd item to top
# unrot3    (X Y Z -- Z X Y)               top item to 3rd
# copy2     (X Y -- X Y X Y)        copy two items on stack
# drop2     (X Y -- )               drop two items from stack
# dip2      (X Y [Op] -- Op X Y)    hide two items from Op
#
# All of these have variants from 2..7. However, a data stack over a few
# elements grows difficult to mentally track or manage, so you should be
# organizing data (into records, lists, etc.) to keep the stack small.

prog rot2 [swap]
prog rot3 [[rot2] dip swap]
prog rot4 [[rot3] dip swap]
prog rot5 [[rot4] dip swap]
prog rot6 [[rot5] dip swap]
prog rot7 [[rot6] dip swap]

assert [1 2 3 4 5 6 7 rot7 1 eq 7 eq 6 eq 5 eq 4 eq 3 eq 2 eq]

prog unrot2 [swap]
prog unrot3 [swap [unrot2] dip]
prog unrot4 [swap [unrot3] dip]
prog unrot5 [swap [unrot4] dip]
prog unrot6 [swap [unrot5] dip]
prog unrot7 [swap [unrot6] dip]

assert [1 2 3 4 5 6 7 unrot7 6 eq 5 eq 4 eq 3 eq 2 eq 1 eq 7 eq]

prog copy2 [[copy ] dip copy [unrot2] dip]
prog copy3 [[copy2] dip copy [unrot3] dip]
prog copy4 [[copy3] dip copy [unrot4] dip]
prog copy5 [[copy4] dip copy [unrot5] dip]
prog copy6 [[copy5] dip copy [unrot6] dip]
prog copy7 [[copy6] dip copy [unrot7] dip]

assert [1 2 3 4 5 6 7 copy7 [7 eq 6 eq 5 eq 4 eq 3 eq 2 eq] copy dip dip 1 eq 1 eq]

prog drop2 [drop drop]
prog drop3 [drop drop drop]
prog drop4 [drop drop drop drop]
prog drop5 [drop drop drop drop drop]
prog drop6 [drop drop drop drop drop drop]
prog drop7 [drop drop drop drop drop drop drop]

assert [1 2 3 4 5 6 7 8 drop7 1 eq]

macro dip2 [p-dip p-dip]
macro dip3 [p-dip p-dip p-dip]
macro dip4 [p-dip p-dip p-dip p-dip]
macro dip5 [p-dip p-dip p-dip p-dip p-dip]
macro dip6 [p-dip p-dip p-dip p-dip p-dip p-dip]
macro dip7 [p-dip p-dip p-dip p-dip p-dip p-dip p-dip]

assert [1 2 3 4 5 6 7 8 9 [2 eq] dip7 9 eq 8 eq 7 eq 6 eq 5 eq 4 eq 3 eq 1 eq]

# CONVENIENCE FUNCTIONS FOR RECORDS AND VARIANTS

# getd          Record Label Default -- Value
#  get with a default value.
prog getd [[drop get] [[drop2] dip] try-else]

assert [42 0 'x put copy 'x 0 getd 42 eq 'y 0 getd 0 eq]

# putd          Value Record Label Default -- Record
#  put with a default value. What this means: 
#   If the Value matches Default, delete Label from Record.
#   Otherwise, set Label to Value in Record.
prog putd [[[rot3] dip eq del] [drop put] try-else]
assert [
    6 7 0 'x put 'y put copy 
    42 swap 'x 42 putd 
    ['y get 6 eq] verify
    ['x get] reject
    drop
    42 swap 'x 0 putd
    ['y get 6 eq] verify
    'x get 42 eq
]

# Constructing records directly from stack items.
# 
# In these cases, we usually have static labels in the local program,
# and values provided later at runtime.
# 
# d0            -- ()
# d1            V1 L1 -- L1:V1
# d2            V1 V2 L1 L2 -- (L1:V1, L2:V2)
# d3            V1 V2 V3 L1 L2 L3 -- (L1:V1, L2:V2, L3:V3)
# d4..d7        similarly

data d0 [0]
prog d1 [d0 swap put]
prog d2 [[rot2 [d1] dip swap] dip put]
prog d3 [[rot3 [d2] dip swap] dip put]
prog d4 [[rot4 [d3] dip swap] dip put]
prog d5 [[rot5 [d4] dip swap] dip put]
prog d6 [[rot6 [d5] dip swap] dip put]
prog d7 [[rot7 [d6] dip swap] dip put]

assert [
    1 2 3 4 5 6 7 
    ['x 'y 'z 'a 'b 'c 'd d7] apply
    copy
    'a get 4 eq
    'x get 1 eq
]

# Pseudo-literal construction of records.
# 
#   d0 'x 6
#   di 'y 7
#   di ... ops on dict
#
# d0          -- ()
# di          (Record Label Value -- Record)    'dict item'
prog di [unrot3 put]

assert [
    d0 'x 11
    di 'y 12
    di 'z 13
    di 'point d1
    copy
    'point get 'y get 12 eq
    'point get 'x get 11 eq
]

# Constructing lists from stack items.
#
#   l0              empty list
#   X l1            list of one item
#   X Y l2          list of two items (X at head)
#   l3..l7
#
# Note that 'li' will be defined in list module 
# (requires acceleration to addend efficiently)
#
data l0 [0]
prog l1 [l0 pair]
prog l2 [l1 pair]
prog l3 [l2 pair]
prog l4 [l3 pair]
prog l5 [l4 pair]
prog l6 [l5 pair]
prog l7 [l6 pair]

assert [0x6f 0x6d 0x6e 0x2d 0x6e 0x6f 0x6d l7 "omn-nom" eq]

prog unl0 [l0 eq]
prog unl1 [unpair unl0]
prog unl2 [unpair unl1]
prog unl3 [unpair unl2]
prog unl4 [unpair unl3]
prog unl5 [unpair unl4]
prog unl6 [unpair unl5]
prog unl7 [unpair unl6]

assert ["omn-nom" copy unl7 l7 eq]

# Variants aka Tagged Unions
#
# We'll represent variants as a singleton record `label:Value`.
# For now just using 'd1' for this. But I could add some dedicated
# names for clarity.
#

# Boolean values.
#   true    0b1      1
#   false   0b0     -1
data true [0b1]
data false [0b0]

# Boolean Conditional Behavior 
# 
# The glas program model favors backtracking conditional behaviors
# over booleans, but in some cases booleans are more convenient.
# 
# if-then-else      [Cond] [OnTrue] [OnFalse] -- OnTrue | OnFalse
# ifb-then-else      Bool  [OnTrue] [OnFalse] -- OnTrue | OnFalse
#
prog p-ifb-then-else [[true eq] unrot3 [false eq] p-prefix p-try-then-else]
prog p-if-then-else [p-ifb-then-else p-suffix]
macro ifb-then-else [p-ifb-then-else]
macro if-then-else [p-if-then-else]

# todo: basic boolean operators?
#       variant conditionals?

# ANNOTATIONS SUPPORT
# 
# Glas programs support annotations under 'prog' header:
#
#     prog:(do:Program, Annotations)
#
# Unfortunately, the g0 does not provide a convenient syntax to
# annotate programs (e.g. with type information). The closest we
# have is something like this:
#
#     prog example [[Program] Annotations annotate apply]
# 
# This is certainly awkward. But improving syntax for annotations
# can wait for development of a future language module.

# p-prog-body       Program -- Program
#  for 'prog:(do:P)' returns result of `P p-prog-body`
#  for anything else, returns initial value
prog p-prog-body [['prog get 'do p-nop getd] loop]

# p-prog-anno       Program -- Annotations
#  for 'prog:(do:P, Annotations)' returns Annotations
#  for anything else, returns empty record
prog p-prog-anno [['prog get 'do del] [drop d0] try-else]

assert [
    d0 
    'x 'a di 
    'y 'b di
    copy
    'do [p-prog-body] di
    'prog d1
    p-prog-anno
    eq
]


# annotate   Program Annotations -- Program
#  set header annotations for a program. Or clear annotations
#  if an empty record of annotations is set.
prog annotate [
    [p-prog-body] dip
    ['do del 0 eq]['do put 'prog d1] try-else
]

prog p-anno-update [
    [p-prog-anno] p-prefix  
    p-dip [copy] p-prefix 
    [swap annotate] p-suffix
]

# anno-update     [Annotations -- Annotations]  =>  [Program] -- [Program]
macro anno-update [p-anno-update]

assert [
    'x 'y
    p-nop
    [unrot3 di] anno-update
    p-prog-anno
    'x get 'y eq
]

# p-anno-put       ([Program] Value Label -- [Program](Label:Value))
#  add a specific annotation to a program.
prog p-anno-put [rot3 [swap put] anno-update]

assert [
    p-nop 'y 'x p-anno-put
    p-prog-anno 'x get 'y eq
]    

# p-anno-get       ([Program](Label:Value) Label -- Value)
prog p-anno-get [[p-prog-anno] dip get]

# p-anno-getd      ([Program](Label?_) Label Value -- Value)
#  get annotated value with a default.
prog p-anno-getd [[p-prog-anno] dip2 getd]

# p-accel      ([Program] Model -- [Program](accel:Model))
#
#  Example usage: 
#       prog list-append [[list-append-impl] 'list-append p-accel apply]
#
#  Acceleration is an essential performance annotation for Glas systems.
#  It informs a compiler to substitute a subprogram with a highly optimized
#  implementation that can take advantage of underlying representations and
#  other hidden details. This supports extension with performance features.
#
prog p-accel ['accel p-anno-put]
prog p-accel-get ['accel p-anno-get]

# p-accel-opt
#
# Same as p-accel, but also marks acceleration optional. A compiler 
# or interpreter may ignore optional acceleration if unrecognized 
# or unsupported.
#
prog p-accel-opt ['opt d1 p-accel]

# p-arity      ([Program] N N -- [Annotated Program])
#
# Arity annotations are a super lightweight type annotation that only
# examines the number if input and output elements on the data stack.
# All glas programs should have static arity.
#
prog p-arity ['i 'o d2 'arity p-anno-put]
prog p-arity-get ['arity p-anno-get copy 'i get swap 'o get]

assert [
    42 p-nop p-curry 
    14 15 p-arity 
    'xyzzy p-accel
    copy 
    p-arity-get pair 14 15 pair eq
    p-accel-get 'xyzzy eq
]

# Further TODO:
#
# Memoization
# Stowage
# Type Annotations (likely another module)
# 

# Lightweight Profiling Annotations
#  prof(chan:ChanId)
prog p-prof-chan ['chan d1 'prof p-anno-put]


# RUNTIME ASSERTIONS
#
# The 'always' and 'never' ops are similar to 'verify' and 'reject'
# except that the outcome cannot be observed. On failure, evaluation
# halts instead with a given debug message.
#
# Ideally, these assertions should be avoided in regular code, perhaps
# replaced by type annotations and static analysis. But temporary use
# for debugging purposes can be useful.
#
prog p-never-msg [
    [p-reject] dip
    0 'assertion put p-halt-with-msg
    p-try-else
]
prog p-always-msg [
    [p-reject] dip
    0 'assertion put p-halt-with-msg
    p-try-then
]

macro always-msg [p-always-msg]
macro always [0 p-always-msg]
macro never-msg [p-never-msg]
macro never [0 p-never-msg]

assert [0 [0 eq] always drop]
assert [0 [1 eq] never  drop]


# Effectful NOP
#
# Effectfully does nothing. This may be useful to control 
# partial evaluation in context of profiling. 
prog eff-nop [['nop eff] p-reject p-try apply]
assert [eff-nop]

# BASIC LOGGING
#
# Logging is convenient for debugging or tracking progress of an application.
# This can be expressed using 'log:Message' effects, where messages are simple
# records such as `(lv:warn, text:"there is an issue")` to enable extensions.
#
# A runtime can potentially inject some extra context into or around the log
# message before it reaches the user, such as a copy of the data stack or the 
# name of the file being compiled.
#

# log-send      Message --
#  Sends provided Message as a logging effect, dropping unit result. 
#  Failure is silently ignored.
prog log-send ['log d1 [eff 0 eq][drop] try-else]

# log message builders
#   logmsg-text-lv         "Hello" 'warn -- (lv:warn, text:"Hello")
#   logmsg-val-text-lv   42 "Hello" 'warn -- (lv:warn, text:"Hello", val:42)
# Will drop an empty/unit text, level, or value field.
prog logmsg-text-lv [0 'lv 0 putd 'text 0 putd]
prog logmsg-val-text-lv [logmsg-text-lv 'val 0 putd]

# "Message" log-info  (or log-warn or log-error)
prog log-info ['info logmsg-text-lv log-send]
prog log-warn ['warn logmsg-text-lv log-send]
prog log-error ['error logmsg-text-lv log-send]

# Value "Message" log-info-v  (or log-warn-v or log-error-v)
prog log-info-v ['info logmsg-val-text-lv log-send]
prog log-warn-v ['warn logmsg-val-text-lv log-send]
prog log-error-v ['error logmsg-val-text-lv log-send]

# LOADING MODULES OR DEFINITIONS
#
# The 'load' effect is supported by language modules and glas CLI apps.
# The essential idea is to obtain a value from the module system. The
# basic targets are 'global:"ModuleName"' or 'local:"ModuleName". The
# g0 compiler extends this to further support 'dict' and 'dict:word'.
#

# ModuleRef load-ref (any valid ref)
prog load-ref  [ 'load d1 eff ]

# load-dict (compile-time effect for g0)
# Load current dictionary as data. 
# Excludes words defined later within file.
prog load-dict [ 'dict load-ref ]

# 'word load-word (compile-time effect for g0)
# This will load a definition as data. 
prog load-word [ 'dict d1 load-ref ]

# "ModuleName" load-global (load a local module)
prog load-global [ 'global d1 load-ref ]

# "ModuleName" load-local (load a global module)
prog load-local [ 'local d1 load-ref ]
